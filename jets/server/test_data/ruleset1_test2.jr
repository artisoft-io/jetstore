# ///////////////////////////////////////////////////////////////////////////////////////
# Unit Test JetStore server process
# ---------------------------------------------------------------------------------------
import "support_test2.jr"

# =======================================================================================
# Create Population
# ---------------------------------------------------------------------------------------
jetstore_config {
  $max_looping = 4
};

# ========================================================================
# Loop Control
# -----------------------------------------------------------------------
# Create Member / Patient
# -----------------------------------------------------------------------
volatile_resource newPatient = "newPatient";
volatile_resource patientPersonaRow = "patientPersonaRow";
volatile_resource claim_group_lk_key = "claim_group_lk_key";

[LOOP020]:
  (?simspec rdf:type aspec:Simulator).
  (?simspec aspec:nbr_entities ?max).
  (?simspec aspec:entity_key_prefix ?patient_start_key).
  (?state rdf:type jets:State).
  (?state jets:loop ?i).
  [?i <= ?max]
  ->
  (?state newPatient (create_entity (?patient_start_key + (?i apply_format "%03u"))))
;

# Note: Adding +1 to the max loop to ensure when that when jets:completed is set 
#       all the entities are created and we're ready to do 'pick_one' on local lists
[LOOP030]:
  (?simspec rdf:type aspec:Simulator).
  (?simspec aspec:nbr_entities ?max).
  (?state rdf:type jets:State).
  (?state jets:loop ?i).
  [?i >= (?max + 1)]
  ->
  (?state jets:completed true)
;


# ========================================================================
# Create Patients & Claims
# -----------------------------------------------------------------------
[MEC01]:
  (?simspec rdf:type aspec:Simulator).
  (?simspec aspec:entity_persona_lk ?uri).
  (?state rdf:type jets:State).
  (?state newPatient ?patient).
  (?patient jets:key ?key)
  ->
  (?patient rdf:type asim:Entity).
  (?patient rdf:type hc:SimulatedPatient).
  (?patient patientPersonaRow (lookup_rand ?uri)).
  (?patient hc:patient_number ?key)
;

[MEC210]:
  (?patient rdf:type asim:Entity).
  (?patient patientPersonaRow ?persona_row).
  (?persona_row asim:persona_key ?persona_key).
  (?persona_row asim:event_group1_lk ?event_group1_lk).
  (?persona_row asim:demographic_group_key ?demo_group)
  (?persona_row asim:description ?desc)
  ->
  (?patient asim:persona_key ?persona_key).
  (?patient asim:event_group1_lk ?event_group1_lk).
  (?patient asim:demographic_group_key ?demo_group).
  (?patient asim:description ?desc)
;
